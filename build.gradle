plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.5'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.sonarqube' version '5.1.0.4882'
	id 'jacoco'
}

group = 'com.fabrica.p6f5'
version = '0.0.1-SNAPSHOT'
description = 'API REST para facturación y pagos de proyecto 6'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
	implementation 'io.jsonwebtoken:jjwt-impl:0.11.5'
	implementation 'io.jsonwebtoken:jjwt-jackson:0.11.5'
	implementation 'org.postgresql:postgresql'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-graphql'
	implementation 'org.flywaydb:flyway-core'
	implementation 'org.flywaydb:flyway-database-postgresql'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0'
	
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	
	implementation 'com.itextpdf:itext7-core:8.0.2'
	
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'org.springframework.graphql:spring-graphql-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

jacoco {
	toolVersion = "0.8.11"  // Versión más reciente
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required = true
		xml.outputLocation = file("${layout.buildDirectory.get()}/reports/jacoco/test/jacocoTestReport.xml")
		html.required = true
		html.outputLocation = file("${layout.buildDirectory.get()}/reports/jacoco/test/html")
	}
}

sonar {
	properties {
		property "sonar.projectKey", "danielarodriguez4_billing-and-payments-p6f5"
		property "sonar.organization", "danielarodriguez4"
		property "sonar.host.url", "https://sonarcloud.io"
		property "sonar.java.source", "21"
		property "sonar.java.target", "21"
		property "sonar.coverage.jacoco.xmlReportPaths", "${layout.buildDirectory.get()}/reports/jacoco/test/jacocoTestReport.xml"
		property "sonar.exclusions", "**/config/**,**/entity/**,**/dto/**"
	}
}

tasks.named('sonarqube') {
	dependsOn jacocoTestReport
}
